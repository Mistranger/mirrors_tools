1 'CMD SAVE"B:NO12"
10 CLEAR ,&H8FFF:DEFINT A-Z
20 GOSUB 9998
30 POLL E,&H00,&H77,&HB7,&H77
500 C1$="アンドリュ−が話す。":C2$="マ−チンが話す。":C3$="デビッドが話す。":C4$="スシャ−ナが話す。":C5$="アランが話す。":C6$="外の景色を見る。"
1000 GOSUB 8040:GOSUB 8020:GOSUB 8000:GOSUB 8010:GOSUB 9020:GOSUB 6290:GOSUB 6340
2220 BM$="デビッドはカ−ドゲ−ムをやめて外の風景を見ていると、スシャ−ナが、心配そうに話かけてきた。":GOSUB 5100
2240 BM$="スシャ−ナ「・・・どうしたの、デビッド。暗いじゃない」":GOSUB 5100:G1=0:G2=0
2241 CM=2:CM$(1)=C3$:CM$(2)=C4$:GOSUB 5400
2247 IF CN=2 AND G1=1 THEN 2270
2248 IF CN=1 THEN GOSUB 2250:G1=1:GOTO 2241
2249 IF CN=2 THEN GOSUB 2265:GOTO 2241
2250 BM$="デビッド「あ、いや、何でもないよ。チョット夢見が悪くて・・・ハハハ・・・」":GOSUB 5100
2260 BM$="デビッドは無理して笑って見せる・・・。":GOSUB 5100:RETURN
2265 BM$="スシャ−ナ「一人で悩むなんて、私に話してくださる？」":GOSUB 5100:RETURN
2270 BM$="スシャ−ナ「顔色が悪いわ。一体どんな夢を見たの？」":GOSUB 5100
2280 BM$="スシャ−ナは好奇心と心配のいり混じった目でデビッドを見ている。":GOSUB 5100
2282 GOSUB 6400:GOSUB 6300
2285 BM$="バスは草原を走っている。デビッドは、誰かに話せば少しは気が楽になるように思えたので、夢の出来事を最初から話しだした。":GOSUB 5100
2286 BM$="いつの間にかデビッドの夢の話にメンバ−達もゲ−ムを忘れ聞きいっている。":GOSUB 5100:G1=0:G2=0:G3=0:G4=0:CM=2
2290 '
2291 IF G4=1 AND G5=1 THEN G6=1:CM=3
2292 IF G1=1 AND G2=1 THEN G3=1:CM$(1)=C4$:CM$(2)=C5$:CM$(3)=C6$:GOSUB 5400 :ELSE CM$(1)=C1$:CM$(2)=C2$:GOSUB 5400
2293 IF CN=3 THEN 2360
2296 IF CN=2 AND G3=1 THEN G5=1:GOSUB 2330:GOTO 2290
2297 IF CN=1 AND G3=1 THEN G4=1:GOSUB 2320:GOTO 2290
2298 IF CN=1 THEN G1=1:GOSUB 2300:GOTO 2290
2299 IF CN=2 THEN G2=1:GOSUB 2310:GOTO 2290
2300 GOSUB 6330:BM$="アンドリュ−「そいつは確かにうなされるぜ」":GOSUB 5100:RETURN
2310 GOSUB 6310:BM$="マ−チン「クレアの事が原因じゃないかな・・・元気だせよ、デビッド」":GOSUB 5100:RETURN
2320 GOSUB 6340:BM$="スシャ−ナ「そういえば今朝ニュ−スでイヤな事件を報道していたわ。なんでもル−アンで僧侶が殺されてたそうよ」":GOSUB 5100:IF G6=1 THEN GOSUB 2340:RETURN :ELSE RETURN
2330 GOSUB 6320:BM$="アラン「本当かい！？　もしかしたら、デビッドの夢は予知夢なのかもな」":GOSUB 5100:IF G6=1 THEN GOSUB 2340:RETURN :ELSE RETURN
2340 BM$="・・・メンバ−のみんなは、それぞれ勝手なこと言っている。":GOSUB 5100:RETURN
2350 '------------------------------------------------------------------
2360 GOSUB 6260:BM$="デビッドは、みんなの見解を上の空で聞きながら窓から外の景色を見た・・・。":GOSUB 5100
2365 COMMON FO:POLL P,&H05,&H2F:POLL P,&H0105,&H2F
2370 GOSUB 6270:BM$="デビッド「んっ？・・・何だ！？・・・」":GOSUB 5100
2380 GOSUB 6280:BM$="後から白馬にまたがった白い騎士が追いかけて来るのが見えた・・・ような気がした。":GOSUB 5100
2390 BM$="デビッドは思った。「・・・馬の上に白騎士？　まさか！？・・・透けている、幽霊！？・・・」":GOSUB 5100
2400 GOSUB 6380:GOSUB 6345:GOSUB 8010:BM$="デビッドはあわてて振り向いたが、後には白い騎士ではなく車が走っていた。":GOSUB 5100:G1=0:G2=0
2401 CM=2:CM$(1)=C3$:CM$(2)=C4$:GOSUB 5400:GOTO 2404
2402 CM=2:CM$(1)=C3$:CM$(2)=C1$:GOSUB 5400
2404 IF CN=2 AND G1=3 THEN 2460
2405 IF CN=1 AND G2=2 THEN GOSUB 2450:G1=3:GOTO 2402
2406 IF CN=2 AND G1=1 THEN GOSUB 2440:G2=2:GOTO 2401
2407 IF CN=1 AND G2=1 THEN GOSUB 2430:G1=1:GOTO 2401
2408 IF CN=2 THEN GOSUB 2420:G2=1:GOTO 2401
2409 IF CN=1 THEN GOSUB 2410:GOTO 2401
2410 GOSUB 6345:BM$="デビッドは、つぶやくように思った。「車！？・・・幻覚か」":GOSUB 5100:RETURN
2420 GOSUB 6340:BM$="スシャ−ナ「デビッド何か言った？　そんなに驚いた顔をして、何かあったの？」":GOSUB 5100
2425 BM$="スシャ−ナはデビッドが、「車・・・幻覚か・・・」と、つぶやいたように聞こえた。":GOSUB 5100:RETURN
2430 GOSUB 6345:BM$="デビッド「いや、なんでもない・・・マ−チンの言うとおり、クレアの事で少し疲れたみたいだ・・・」":GOSUB 5100:RETURN
2440 GOSUB 6340:BM$="スシャ−ナ「カルナックまでまだあるわ。大丈夫？」":GOSUB 5100:RETURN
2450 GOSUB 6345:BM$="デビッド「心配しなくても大丈夫だ。少し休めば元に戻れるさ」":GOSUB 5100:RETURN
2460 GOSUB 6330:BM$="アンドリュ−「スシャ−ナ！！　デビッドは疲れているから一人にしてやれ、俺達はゲ−ムを続けよう。早くカ−ドを配れよ」":GOSUB 5100
2470 GOSUB 6340:BM$="スシャ−ナ「はいはい、わかりました」":GOSUB 5100
2480 COMMON FO:GOSUB 8010:GOSUB 6390:BM$="メンバ−達の乗ったバスは目的地カルナックえと走りつづけた。":GOSUB 5100
2485 '----------------
2490 GOSUB 6370:BM$="デビッドが見た車には、探偵のクラ−クとヴィンスが乗っている。":GOSUB 5100:G1=0:G2=0:GOSUB 6360:GOSUB 2515
2491 CM=2:CM$(1)="クラ−クが話す。":CM$(2)="ヴインスが話す。":GOSUB 5400
2492 '
2495 IF CN=1 AND G2=2 THEN GOSUB 2530:G1=2:GOTO 2491
2496 IF CN=2 AND G1=1 THEN GOSUB 2520:G2=2:GOTO 2491
2497 IF CN=1 AND G2=1 THEN GOSUB 2510:G1=1:GOTO 2491
2498 IF CN=2 THEN GOSUB 2500:G2=1:GOTO 2491
2499 IF CN=1 THEN GOSUB 2515:GOTO 2491
2500 GOSUB 6342:BM$="ヴィンス「所長、今朝のニュ−ス見ましたか？」":GOSUB 5100:RETURN
2510 GOSUB 6341:BM$="クラ−ク「あぁ、フランスもなかなか物騒だな」":GOSUB 5100:RETURN
2515 GOSUB 6341:BM$="クラ−ク「奴等の目的地は［カルナック］だ」":GOSUB 5100:RETURN
2520 GOSUB 6342:BM$="ヴィンス「あの事件、エレノヴィジョン達と何か関係あるんじゃないですか？　あの連中の近くで、やたらと人が死ぬなんて」":GOSUB 5100:RETURN
2530 GOSUB 6341:BM$="クラ−ク「さあな。　ル−アンは逆方向だし、奴等に僧侶は向いて無いさ」":GOSUB 5100
2540 COMMON FO:BM$="クラ−クは関係なしと見ているようだ・・・。":GOSUB 5100
2545 GOSUB 6410
2550 ISET X:BM$="一行はフランスの秘境ブルタ−ニュを知るうえで通り過ごせない町［ヴァンヌ］を通過し、ツア−目的地である［カルナック］についた。":GOSUB 5100
2560 BM$="カルナックには、先史時代のメンヒルが多数存在する。その巨石群の中に設けられた特設ステ−ジで今夜、ヨ−ロッパツア−が幕を開ける。":GOSUB 5100
2570 GOSUB 5200
3000 CMD RUN "LIV1"
4999 '
5000 GOSUB 5300
5005 BN=&HF0C2:KN=BN:KX=0:FOR K1=1 TO LEN(BM$) STEP 2:K$=MID$(BM$,K1,2):CMD WIDTH KN,&H20,7:CMD KANJI K$:KN=KN+2:KX=KX+1
5010 IF KX=38 THEN KX=0:BN=BN+&H50*11:KN=BN
5020 FOR K2=0 TO 2:BEEP 1:BEEP 0:NEXT:FOR L=1 TO PEEK(&HE3FF):NEXT:NEXT:RETURN
5100 GOSUB 5000:A=&H35D9:CALL A
5105 BM$="▽":CMD WIDTH &HFB34,&H02,7:CMD KANJI BM$
5110 I$=INKEY$:IF I$=CHR$(13) THEN GOSUB 5300:RETURN :ELSE 5110
5200 GOSUB 5610:CLS 2:COLOR=(7,7):RETURN
5205 GOSUB 5610:CLS 2:RETURN
5210 FOR I=1 TO 20:FOR S=1 TO 100:NEXT:FOR L=1 TO 10:BEEP 1:BEEP 0:NEXT:NEXT:RETURN
5300 LINE (0,151)-(639,199),0,BF:RETURN
5400 GOSUB 5300
5410 BN=&HF0D2:CN=0:FOR I=1 TO CM:CMD WIDTH BN,&H20,7:CMD KANJI CM$(I):BN=BN+&H50*13:NEXT
5420 LINE(110,156+CN*13)-(512,166+CN*13),7,BF,XOR:CN2=CN
5430 I$=INKEY$:IF I$="" THEN 5430
5431 IF I$=CHR$(27) THEN GOSUB 10000:GOTO 5400
5435 IF I$="8" THEN IF CN=0 THEN 5430 :ELSE CN=CN-1:GOTO 5440
5436 IF I$="2" THEN IF CN=CM-1 THEN 5430 :ELSE CN=CN+1
5440 LINE(110,156+CN2*13)-(512,166+CN2*13),7,BF,XOR
5450 IF I$=CHR$(13) THEN CN=CN+1:GOSUB 5300:RETURN :ELSE 5420
5500 FOR I=1 TO 7000:NEXT:RETURN
5510 FOR I=1 TO 3000:NEXT:RETURN
5530 FOR L=1 TO 500:NEXT:RETURN
5599 '
5600 CMD COLOR[0,0,0,0,0,0,0,0]:RETURN
5610 CMD COLOR[F,0,0,0,0,0,0,0,0]:RETURN
5800 CMD COLOR[F,0,89,169,242,315,380,445,511]:RETURN:'ｾﾋﾟｱ
5810 CMD COLOR[M,0,72,144,224,304,377,444,511]:RETURN:'ﾋﾙ ｷｲﾛ
5820 CMD COLOR[M,0,1,67,132,205,351,431,511]:RETURN:'ｱｵ
5830 CMD COLOR[M,0,138,211,284,357,429,502,511]:RETURN
5840 CMD COLOR[M,0,130,195,260,325,390,487,511]:RETURN:'ｿﾗ
5850 CMD COLOR[M,0,138,211,284,357,429,502,511]:RETURN
6000 '
6260 GOSUB 5205:CMD LOAD "ﾊﾞｽD",&H00,&HC00E:GOSUB 5850:RETURN
6270 CMD LOAD "ｸﾓD",&H00,&HC00E:GOSUB 5850:RETURN
6280 GOSUB 5205:CMD LOAD "ｳﾏ",&H00,&HC00E:GOSUB 9000:GOSUB 8060:GOSUB 8070:GOSUB 5850:RETURN
6290 GOSUB 5205:CMD LOAD "ﾊﾞｽD",&H00,&HC001:GOSUB 5850:RETURN
6300 IF W1=0 THEN W1=1:W2=0:W3=0:W4=0:W5=0:WEND LOAD"davR",AR:RETURN :ELSE RETURN
6310 IF W2=0 THEN W1=0:W2=1:W3=0:W4=0:W5=0:WEND LOAD"marL",AL:RETURN :ELSE RETURN
6320 IF W3=0 THEN W1=0:W2=0:W3=1:W4=0:W5=0:WEND LOAD"alnL",AL:RETURN :ELSE RETURN
6330 IF W4=0 THEN W1=0:W2=0:W3=0:W4=1:W5=0:WEND LOAD"andL",AL:RETURN :ELSE RETURN
6340 IF W5=0 THEN W1=0:W2=0:W3=0:W4=0:W5=1:WEND LOAD"linR",AR:RETURN :ELSE RETURN
6341 IF W6=0 THEN W6=1:W7=0:WEND LOAD"ｸﾗｰｸ",AL:RETURN :ELSE RETURN
6342 IF W7=0 THEN W7=1:W6=0:WEND LOAD"ｳｲﾝｽ",AR:RETURN :ELSE RETURN
6345 IF W1=0 THEN W1=1:W2=0:W3=0:W4=0:W5=0:WEND LOAD"davL",AL:RETURN :ELSE RETURN
6350 GOSUB 5205:CMD LOAD "ｸﾙﾏ",&H00,&HC00E:GOSUB 5850:RETURN
6360 GOSUB 5205:CMD LOAD "Kﾏｴ",&H00,&HC00E:GOSUB 5850:RETURN
6370 GOSUB 5205:GOSUB 9010:GOSUB 8050:CMD LOAD "Kｳｼﾛ",&H00,&HC00E:GOSUB 5850:RETURN
6380 POLL X:COMMON FO:GOSUB 5205:ISET X:CMD LOAD "ﾊﾞｽ2":CMD PUT &H3000,1,&H00,&HC00E:GOSUB 8030:GOSUB 9020:GOSUB 5850:RETURN
6390 GOSUB 5205:CMD LOAD "ﾊﾞｽ3",&H00,&HC00E:GOSUB 5850:RETURN
6400 GOSUB 5205:CMD LOAD "ﾊﾞｽ4",&H00,&HC00E:GOSUB 5850:RETURN
6410 GOSUB 5205:ISET X:CMD LOAD "ｶﾙﾅ",&H00,&HC00E:GOSUB 9030:GOSUB 5820:RETURN
7999 '
8000 ISET X:CMD LOAD "Pﾊﾞｽ":ISET L,&H3000,&H7FB0,0,&H00:RETURN
8010 ISET S,&H1000,1,&H50:ISET P,0,&H00,0,&H1000:RETURN
8020 ISET X:CMD LOAD "Pｻｶ":ISET L,&H3000,&H7FB0,1,&H01:RETURN
8030 ISET S,&H2200,1,&H7F:ISET P,1,&H00,1,&H1000:RETURN
8040 ISET X:CMD LOAD "Pｸﾙﾏ":ISET L,&H3000,&H3FF0,2,&H00:RETURN
8050 ISET S,&H1200,1,&H50:ISET P,2,&H00,2,&H03A0:RETURN
8060 ISET X:CMD LOAD"Pｳﾏ":ISET L,&H3000,&H37F8,3,&H00:RETURN
8070 ISET S,&H2000,1,&H6F:ISET P,3,&H00,3,&H01C0:RETURN
9000 COMMON FM:COMMON P"15:37"TO"17:29":COMMON FIF:RETURN
9010 COMMON FM:COMMON P"12:45"TO"15:19":COMMON FI:RETURN
9020 COMMON FM:COMMON P"25:42"TO"28:34":COMMON FI:RETURN
9030 COMMON FM:COMMON P"09:15"TO"11:52":COMMON FI:RETURN
9998 COLOR 0:LOCATE 0,17:PRINT"ャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャャ"
9999 LOCATE 0,18:PRINT"                                                                                ":RETURN
10000 BM$="ＤＡＴＡをＳＡＶＥしますか？　Ｙ／Ｎ":GOSUB 5000
10010 S$=INKEY$:IF S$="" THEN 10010
10015 IF S$="N" OR S$="n" THEN RETURN
10020 IF S$="Y" OR S$="y" THEN 10040
10030 GOTO 10010
10040 BM$="ドライブ１に、ＳＡＶＥ・ディスクを入れて下さい。":GOSUB 5100
10049 CMD SET "A:"
10050 CMD PEEK &HF3FE,TU:CMD PEEK &HF3FF,TI
10060 IF TU=200 AND TI=201 THEN 10080
10070 BM$="ドライブ１のディスクが違います。":GOSUB 5100:GOTO 10040
10080 POKE &HE302,12:POKE &HE303,0:'SAVE NO
10090 CMD WRITE &H00,&H00,&H01,&HE300
10110 BM$="ＳＡＶＥしました。ドライブ１のディスクに、ＭＡＩＮディスクを入れて下さい。":GOSUB 5100
10120 CMD SET "A:"
10130 CMD PEEK &HF3FE,TU:CMD PEEK &HF3FF,TI
10140 IF TU=100 AND TI=101 THEN RETURN
10150 BM$="ドライブ１のディスクが違います。":GOSUB 5100:GOTO 10110
