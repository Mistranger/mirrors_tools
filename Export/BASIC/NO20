1 'CMD SAVE "B:NO20"
10 CLEAR ,&H8FFF:DEFINT A-Z:CLS
1000  CM=2:C1$="デビッドは考える。":C2$="町並みを見る。":C3$="石造りの町並みを歩く"
1005 GOSUB 9520:GOSUB 9510:GOSUB 9500
1010 GOSUB 9000:GOSUB 6160:C6$="デビッドが話す。":C7$="占い師の声を聞く。"
1280 'ﾐｷﾞ  ----------------------------
1290 BM$="左の道を行く":GOSUB 5100:ISET X
1300 G1=0:G2=0:G3=0:G4=0:G5=0:CM=2:CM$(1)=C1$:CM$(2)=C2$
1310 GOSUB 5400
1320 IF CN=3 AND G1=3 THEN G1=2:GOTO 1560
1330 IF CN=2 AND G1=3 THEN GOSUB 1710:GOTO 1310
1340 IF CN=1 AND G1=3 THEN GOSUB 1480:GOTO 1310
1350 IF CN=3 AND G1=2 THEN G1=3:GOTO 1560
1360 IF CN=2 AND G1=2 THEN GOSUB 1690:GOTO 1310
1370 IF CN=1 AND G1=2 THEN GOSUB 1480:GOTO 1310
1380 IF CN=3 AND G1>0 THEN GOSUB 1440:G1=2:GOTO 1310
1390 IF CN=2 THEN GOSUB 1420:G3=1:GOTO 1410
1400 IF CN=1 THEN GOSUB 1430:G2=1:GOTO 1410
1410 IF G2=1 AND G3=1 THEN G1=1:CM=3:CM$(3)=C3$:GOTO 1310 :ELSE GOTO 1310
1420 BM$="まっすぐな道に占いの看板が左右に並んでいる。全ての家が占い師の店だ。":GOSUB 5100:RETURN
1430 BM$="・・・なんだ、ここは。　占いの街か？・・・。"::GOSUB 5100:RETURN
1440 GOSUB 9505:BM$="異様な感じであったが、デビッドはその通りを歩きだした。":GOSUB 5100
1450 CM$(2)=C7$:BM$="左側の家の扉が開き、陰気な声が流れて来た・・・。":GOSUB 5100
1460 GOSUB 9515:BM$="「私は占い師です・・・ようこそ・・・魔王様・・・」":GOSUB 5100
1470 BM$="デビッド「えっ！？・・・」":GOSUB 5100
1475 CM=2:GOSUB 6300:BM$="デビッドが開いた扉の中を見ると、地面から手だけが出て動き、人はいなかった・・・。":GOSUB 5100:RETURN
1480 'ｸﾘｶｴｼ
1490 IF GN=4 AND G1=3 THEN BM$="デビッド「俺が悪魔だと？　何故だ？・・・」":GOSUB 5100:GN=0:RETURN
1500 IF GN=4 AND G1=2 THEN BM$="デビッド「俺が魔王だと？　何故だ？・・・」":GOSUB 5100:GN=0:RETURN
1510 IF GN=3 THEN BM$="デビッド「俺はデビッドだ。正体を現せ！！」":GOSUB 5100:GN=4:RETURN
1520 IF GN=2 THEN BM$="デビッド「お前は、俺を知っているのか？・・・」":GOSUB 5100:GN=3:RETURN
1530 IF GN=1 THEN BM$="デビッド「お前は、何者だ？・・・」":GOSUB 5100:GN=2:RETURN
1540 BM$="デビッド「手だけの占い師か、姿を現せ！！」":GOSUB 5100:GN=1:RETURN
1550 'ｲｸ
1560 GA=GA+1:IF GA=6 THEN 2990
1570 GOSUB 9505:CM=2:BM$="デビッドは、訳が判らず気味が悪くなってきたので、無視して先に進む・・・。":GOSUB 5100
1580 IF GA=1 THEN GOSUB 6130:GOSUB 1800:GOSUB 6310
1590 IF GA=2 THEN GOSUB 6120:GOSUB 1800:GOSUB 6320
1600 IF GA=3 THEN GOSUB 6110:GOSUB 1800:GOSUB 6330
1610 IF GA=4 THEN GOSUB 6150:GOSUB 1800:GOSUB 6340
1620 IF GA=5 THEN GOSUB 6180:GOSUB 1800:GOSUB 6350
1630 '
1640 IF G1=2 THEN 1650 :ELSE 1670
1650 BM$="左側の家の扉が開き、陰気な声が流れてきた・・・。":GOSUB 5100
1660 GOSUB 9515:BM$="左からの声「・・・ようこそ・・・魔王様・・・私は占い師です」":GOSUB 5100:GOTO 1310
1670 BM$="右側の家の扉が開き、吐き出すような声が流れてきた・・・。":GOSUB 5100
1680 GOSUB 9525:BM$="右からの声「俺は占い師だ！　ついに来たな。　異端の悪魔め！」":GOSUB 5100:GOTO 1310
1690 GOSUB 9515:BM$="左からの声「・・・ようこそ・・・魔王様・・・」":GOSUB 5100
1700 BM$="同じ言葉が流れるだけで、占い師の手が歓迎しているようにうごいている。":GOSUB 5100:CM=3:GOTO 1310
1710 GOSUB 9525:BM$="右からの声「来たな。　異端の悪魔め！」":GOSUB 5100
1720 CM=3:BM$="吐き捨てるような言葉は、同じ言葉しか言わず、地面から出た手はデビッドを追い払うように動いている。":GOSUB 5100:GOTO 1310
1800 POLL X:BM$="デビッドは通りを歩き続けた。":GOSUB 5100:RETURN
1810 '----
2990 COMMON FO:GOSUB 5200
3000 GOSUB 5205:ISET X:POLL X:CMD RUN"20-1"
4999 '
5000 GOSUB 5300
5005 BN=&HF0C2:KN=BN:KX=0:FOR K1=1 TO LEN(BM$) STEP 2:K$=MID$(BM$,K1,2):CMD WIDTH KN,&H20,7:CMD KANJI K$:KN=KN+2:KX=KX+1
5010 IF KX=38 THEN KX=0:BN=BN+&H50*11:KN=BN
5020 FOR K2=0 TO 2:BEEP 1:BEEP 0:NEXT:FOR L=1 TO PEEK(&HE3FF):NEXT:NEXT:RETURN
5100 GOSUB 5000:A=&H35D9:CALL A
5105 BM$="▽":CMD WIDTH &HFA94,&H02,7:CMD KANJI BM$
5110 I$=INKEY$:IF I$=CHR$(13) THEN GOSUB 5300:RETURN :ELSE 5110
5200 GOSUB 5610:CLS 2:COLOR=(7,7):RETURN
5205 GOSUB 5610:CLS 2:RETURN
5210 FOR I=1 TO 20:FOR S=1 TO 100:NEXT:FOR L=1 TO 10:BEEP 1:BEEP 0:NEXT:NEXT:RETURN
5300 LINE(0,148)-(639,199),0,BF:RETURN
5400 GOSUB 5300
5410 BN=&HF0D2:CN=0:FOR I=1 TO CM:CMD WIDTH BN,&H20,7:CMD KANJI CM$(I):BN=BN+&H50*13:NEXT
5420 LINE(110,156+CN*13)-(512,166+CN*13),7,BF,XOR:CN2=CN
5430 I$=INKEY$:IF I$="" THEN 5430
5431 IF I$=CHR$(27) THEN GOSUB 10000:GOTO 5400
5435 IF I$="8" THEN IF CN=0 THEN 5430 :ELSE CN=CN-1:GOTO 5440
5436 IF I$="2" THEN IF CN=CM-1 THEN 5430 :ELSE CN=CN+1
5440 LINE(110,156+CN2*13)-(512,166+CN2*13),7,BF,XOR
5450 IF I$=CHR$(13) THEN CN=CN+1:GOSUB 5300:RETURN :ELSE 5420
5500 FOR I=1 TO 7000:NEXT:RETURN
5510 FOR I=1 TO 3000:NEXT:RETURN
5530 FOR L=1 TO 500:NEXT:RETURN
5599 '
5600 CMD COLOR[0,0,0,0,0,0,0,0]:RETURN
5610 CMD COLOR[F,0,0,0,0,0,0,0,0]:RETURN
5820 CMD COLOR[M,0,1,67,132,205,351,431,511]:RETURN:'ｱｵ
6000 '
6100 GOSUB 5205:CMD LOAD "ﾍﾞｯﾄ":CMD PUT &H3000,1,&H00,&HC00C:GOSUB 5820:RETURN
6110 GOSUB 5205:CMD LOAD "ﾛｼﾞ":CMD PUT &H3000,1,&H00,&HC00C:GOSUB 5820:RETURN
6120 GOSUB 5205:CMD LOAD "ﾛｼﾞ1",&H00,&HC00C:GOSUB 5820:RETURN
6130 GOSUB 5205:CMD LOAD "ﾛｼﾞ2":CMD PUT &H3000,1,&H00,&HC00C:GOSUB 5820:RETURN
6140 GOSUB 5205:CMD LOAD "ﾛｼﾞ3":CMD PUT &H3000,1,&H00,&HC00C:GOSUB 5820:RETURN
6150 GOSUB 5205:CMD LOAD "ﾛｼﾞ4",&H00,&HC00C:GOSUB 5820:RETURN
6160 GOSUB 9505:GOSUB 5205:CMD LOAD "ﾛｼﾞ5":CMD PUT &H3000,1,&H00,&HC00C:GOSUB 5820:RETURN
6170 GOSUB 9505:GOSUB 5205:CMD LOAD "ﾐﾁ2",&H00,&HC00C:GOSUB 5820:ISET X:RETURN
6180 GOSUB 5205:CMD LOAD "ﾛｼﾞ6":CMD PUT &H3000,1,&H00,&HC00C:GOSUB 5820:RETURN
6298 '&HD181:ﾋﾀﾞﾘ
6299 '&HD1AF:ﾐｷﾞ
6300 GOSUB 9070:CMD LOAD"ﾃ1",&H00,&HD181:ISET X:RETURN
6310 GOSUB 9050:CMD LOAD"ﾃ2",&H00,&HD1AF:ISET X:RETURN
6320 GOSUB 9070:CMD LOAD"ﾃ3",&H00,&HD181:ISET X:RETURN
6330 GOSUB 9050:CMD LOAD"ﾃ4",&H00,&HD1AF:ISET X:RETURN
6340 GOSUB 9070:CMD LOAD"ﾃ5",&H00,&HD181:ISET X:RETURN
6350 GOSUB 9050:CMD LOAD"ﾃ6",&H00,&HD1AF:ISET X:RETURN
9000 COMMON FM:COMMON P"15:25"TO"17:29":COMMON FI:RETURN
9050 POLL E,&H07,&H77,&H77,&H77
9060 POLL P,&H0B,&H01:POLL P,&H0108,&H03:RETURN
9070 POLL E,&H07,&HB7,&HB7,&HB7
9080 POLL P,&H0B,&H01:POLL P,&H0108,&H02:RETURN
9500 ISET X:CMD LOAD "Pｱﾙｸ":ISET L,&H3000,&H5FFF,0,&H00:RETURN
9505 POLL X:ISET X:ISET S,&H1A00,1,&H90:ISET P,0,&H00,0,&H0200:RETURN:'ｱﾙｸ
9506 ISET X:ISET S,&H1A00,1,&H90:ISET P,0,&H00,0,&H0200:FOR I=0 TO 3000:NEXT:RETURN:'ｱﾙｸ
9510 ISET X:CMD LOAD "Pﾖｳｺ":ISET L,&H3000,&H47E8,1,&H00:RETURN
9515 ISET X:ISET S,&H1400,0,&HFF:ISET P,1,&H00,1,&H04C0:RETURN:'ﾖｳｺｿ
9520 ISET X:CMD LOAD "Pｷﾀﾅ":ISET L,&H3000,&H47E8,2,&H00:RETURN
9525 ISET X:ISET S,&H0F00,0,&HFF:ISET P,2,&HF0,2,&H05A0:RETURN:'ｷﾀﾅ
10000 BM$="ＤＡＴＡをＳＡＶＥしますか？　Ｙ／Ｎ":GOSUB 5000
10010 S$=INKEY$:IF S$="" THEN 10010
10015 IF S$="N" OR S$="n" THEN RETURN
10020 IF S$="Y" OR S$="y" THEN 10040
10030 GOTO 10010
10040 BM$="ドライブ１に、ＳＡＶＥ・ディスクを入れて下さい。":GOSUB 5100
10049 CMD SET "A:"
10050 CMD PEEK &HF3FE,TU:CMD PEEK &HF3FF,TI
10060 IF TU=200 AND TI=201 THEN 10080
10070 BM$="ドライブ１のディスクが違います。":GOSUB 5100:GOTO 10040
10080 POKE &HE302,20:POKE &HE303,0:'SAVE NO
10090 CMD WRITE &H00,&H00,&H01,&HE300
10110 BM$="ＳＡＶＥしました。ドライブ１のディスクに、ＭＡＩＮディスクを入れて下さい。":GOSUB 5100
10120 CMD SET "A:"
10130 CMD PEEK &HF3FE,TU:CMD PEEK &HF3FF,TI
10140 IF TU=100 AND TI=101 THEN RETURN
10150 BM$="ドライブ１のディスクが違います。":GOSUB 5100:GOTO 10110
